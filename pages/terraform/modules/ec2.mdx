import Callout from 'nextra-theme-docs/callout'

# tf/ec2/

Esse √© o m√≥dulo respons√°vel pela cria√ß√£o de inst√¢ncias EC2, e
da cria√ß√£o de grupos de seguran√ßa para essas inst√¢ncias.

<Callout emoji="‚ö†Ô∏è">
    Para esse m√≥dulo funcionar como esperado, √© necess√°rio que ele consiga ser executado
    para cada inst√¢ncia presente no arquivo de configura√ß√£o. Para isso, √© necess√°rio que
    ao topo dos recursos utilizados para a cria√ß√£o de inst√¢ncias, seja adicionado o
    atributo `for_each`.
</Callout>

<Callout emoji="üìù">
    O atributo `for_each` √© utilizado para que o Terraform possa executar o mesmo
    recurso para cada item presente no arquivo de configura√ß√£o (como um **for loop** em
    outras linguagens).

    Para mais informa√ß√µes sobre o atributo `for_each`, acesse a
    [documenta√ß√£o oficial](https://www.terraform.io/docs/language/meta-arguments/for_each.html).
</Callout>

Vamos passar por cada recurso presente no arquivo `main.tf` e entender o que ele faz.

## Cria√ß√£o de Chaves P√∫blicas

O primeiro recurso presente no arquivo `main.tf` √© a cria√ß√£o de chaves p√∫blicas para
as inst√¢ncias EC2. Para isso, √© utilizado o recurso `aws_key_pair` em conjunto com `tls_private_key`:

```hcl
resource "tls_private_key" "pk" {
  algorithm = "RSA"
  rsa_bits  = 4096
}

# generate pem file for each instance
resource "aws_key_pair" "kp" {
  for_each = { for instance in var.instances : instance.name => instance }
  key_name   = each.value.name
  public_key = tls_private_key.pk.public_key_openssh
  provisioner "local-exec" {
    command = "echo '${tls_private_key.pk.private_key_pem}' > '${path.module}/keys/${each.value.name}.pem'"
  }
}
```
Ao final, esse recurso cria um arquivo `.pem` para cada inst√¢ncia presente no arquivo e guarda na pasta `keys`.

## Cria√ß√£o de Grupos de Seguran√ßa

A cria√ß√£o de grupos de seguran√ßa √© feita utilizando o recurso `aws_security_group`:

```hcl
resource "aws_security_group" "sg" {
  for_each    = { for sg in var.security_groups : sg.name => sg }
  name        = each.value.name
  description = each.value.description
  vpc_id      = var.vpc_id
  tags = {
    Name = each.value.name
    id   = each.value.id
  }

  dynamic "ingress" {
    for_each = each.value.ingress

    content {
      from_port   = ingress.value.from_port
      to_port     = ingress.value.to_port
      protocol    = ingress.value.protocol
      cidr_blocks = ingress.value.cidr_blocks
    }
  }

  dynamic "egress" {
    for_each = each.value.egress

    content {
      from_port   = egress.value.from_port
      to_port     = egress.value.to_port
      protocol    = egress.value.protocol
      cidr_blocks = egress.value.cidr_blocks
    }
  }
}
```

Esse recurso cria um grupo de seguran√ßa para cada item presente no arquivo de configura√ß√£o, mas ainda n√£o os associa a nenhuma inst√¢ncia.

## Cria√ß√£o de Inst√¢ncias EC2

A cria√ß√£o de inst√¢ncias EC2 √© feita utilizando o recurso `aws_instance`:

```hcl
resource "aws_instance" "ec2_instance" {
  for_each = { for instance in var.instances : instance.name => instance }
  instance_type          = each.value.instance_type
  key_name               = each.value.name
  ami                    = each.value.ami
  subnet_id              = var.public_subnet
  vpc_security_group_ids = [for sg in aws_security_group.sg : sg.id if contains(each.value.security_groups_ids, sg.tags.id)]

  tags = {
    Name = each.value.name
  }

  root_block_device {
    volume_size = 10
  }
}
```

Esse recurso cria uma inst√¢ncia EC2 para cada item presente no arquivo de configura√ß√£o, e associa os grupos de seguran√ßa 
criados anteriormente a cada inst√¢ncia que possua o ID do grupo de seguran√ßa presente no arquivo de configura√ß√£o.

## Cria√ß√£o de EIPs

O √∫ltimo recurso presente no arquivo `main.tf` √© a cria√ß√£o de EIPs para as inst√¢ncias EC2. Para isso, √© utilizado o recurso `aws_eip`:

```hcl
resource "aws_eip" "eip" {
  for_each = { for instance in var.instances : instance.name => instance }
  vpc      = true
  instance = aws_instance.ec2_instance[each.value.name].id
}
```
Esse recurso serve para que as inst√¢ncias EC2 possam ser acessadas de fora da VPC.